MACRO analysis file=figures
opt zfl1

clo 0
h/del 0
opt grid

h/fil 1 run.nt
opt nbox


cd //pawc

cut $21 1>0
cut $22 siglg.f(2,1,4)>50..and.apnum.f(1,0,50)>0
cut $23 siglg.f(2,5,8)>50..and.apnum.f(2,0,50)>0
cut $24 $22.and.$23

do ityp=1, 4
	case [ityp] in

		(1)
		idd=10
		hnam='Theta vs. Phi (Polar)'
		nx=120
		x1=-180
		x2=180
		ny=60
		y1=-180
		y2=180
		vnam='thetcm*180.*sin(phicm)/3.1415%thetcm*180.*cos(phicm)/3.1415'

		(2)
		idd=20
		hnam='Analyzing power '
		nh=400
		x1=0.4
		x2=0.8
		vnam='abs(anpower)'

		(3)
		idd=30
		hnam='Theta CM '
		nh=60
		x1=35
		x2=145
		vnam='thetcm*180./3.1415'

		(4)
		idd=40
		hnam='Phi CM '
		nh=60
		x1=-45
		x2=45
		vnam='phicm*180./3.1415'

	endcase

	id0=1000+[idd]

do ih=1,4
    idh=$SIGMA([id0]+[ih]-1)
    icut=20+[ih]

    if [idd]=10 then
		2dh 5000 'temp' [nx] [x1] [x2] [ny] [y1] [y2]
		nt/proj 5000 //lun1/2.[vnam] $[icut]
		nev = $hinfo(5000,'entries')
		h/del 5000
    else
		1dh 5000 'temp' [nh] [x1] [x2]
		nt/proj 5000 //lun1/2.[vnam] $[icut]
		nev = $hinfo(5000,'entries')
		h/del 5000
    endif

    case [ih] in
        (1) hname='initial '
        (2) hname='left arm '
        (3) hname='right arm '
        (4) hname='coincidence '
    endcase
    hnam1=[hnam]//[hname]//[nev]
    if [idd]=10 then
		2dh [idh] [hnam1] [nx] [x1] [x2] [ny] [y1] [y2]
		nt/proj [idh] //lun1/2.[vnam] $[icut]
    else
		1dh [idh] [hnam1] [nh] [x1] [x2]
		nt/proj [idh] //lun1/2.[vnam] $[icut]
    endif

		h/pl [idh]
		p/print [file]/[hnam1].ps

  enddo

enddo

2dh 1050 'Theta Coincidence vs. Full Signal Sum' 180 80 100 180 0 12000
nt/proj 1050 //lun1/2.siglg.f(2,1,8)%thetcm*180./3.1415 $24
h/pl 1050
p/print [file]/'Theta Coincidence vs. Full Signal Sum'.ps

2dh 1051 'Theta Coincidence vs. Left Arm Signal Sum' 180 80 100 180 0 12000
nt/proj 1051 //lun1/2.siglg.f(2,1,4)%thetcm*180./3.1415 $24
h/pl 1051
p/print [file]/'Theta Coincidence vs. Left Arm Signal Sum'.ps

2dh 1052 'Theta Coincidence vs. Right Arm Signal Sum' 180 80 100 180 0 12000
nt/proj 1052 //lun1/2.siglg.f(2,5,8)%thetcm*180./3.1415 $24
h/pl 1052
p/print [file]/'Theta Coincidence vs. Right Arm Signal Sum'.ps

1dh 1060 'X Coincidence Position: Forward Scattered Electron' 100 -10 0
nt/proj 1060 //lun1/2.hitraxy.f(-1,3,0,3) $24
h/pl 1060
p/print [file]/'X Coincidence Position: Forward Scattered Electron'.ps

1dh 1061 'X Coincidence Position: Reverse Scattered Electron' 100 0 10
nt/proj 1061 //lun1/2.hitraxy.f(-1,3,0,2) $24
h/pl 1061
p/print [file]/'X Coincidence Position: Reverse Scattered Electron'.ps

1dh 1062 'Y Coincidence Position: Forward Scattered Electron' 100 -18 18
nt/proj 1062 //lun1/2.hitraxy.f(-2,3,0,3)/cos(0.1222)+45.25 $24
h/pl 1062
p/print [file]/'Y Coincidence Position: Forward Scattered Electron'.ps

1dh 1063 'Y Coincidence Position: Reverse Scattered Electron' 100 -18 18
nt/proj 1063 //lun1/2.hitraxy.f(-2,3,0,2)/cos(0.1222)+45.25 $24
h/pl 1063
p/print [file]/'Y Coincidence Position: Reverse Scattered Electron'.ps

2dh 1070 'X,Y Coincidence Position: Forward Scattered Electron' 50 -10 10 50 -18 18
nt/proj 1070 //lun1/2.hitraxy.f(-2,3,0,3)/cos(0.1222)+45.25%hitraxy.f(-1,3,0,3) $24
h/pl 1070
p/print [file]/'X,Y Coincidence Position: Forward Scattered Electron'.ps

2dh 1071 'X,Y Coincidence Position: Reverse Scattered Electron' 50 -10 10 50 -18 18
nt/proj 1071 //lun1/2.hitraxy.f(-2,3,0,2)/cos(0.1222)+45.25%hitraxy.f(-1,3,0,2) $24
h/pl 1071
p/print [file]/'X,Y Coincidence Position: Reverse Scattered Electron'.ps

nt/dump 2.thetcm filename=thetcm_i.dat
nt/dump 2.phicm filename=phicm_i.dat
nt/dump 2.thetcm $24 filename=thetcm_f.dat
nt/dump 2.phicm $24 filename=phicm_f.dat
nt/dump 2.siglg.f(2,1,8) $24 filename=ene_t.dat
nt/dump 2.siglg.f(2,1,4) $24 filename=ene_l.dat
nt/dump 2.siglg.f(2,5,8) $24 filename=ene_r.dat
nt/dump 2.siglg.f(2,1,1) $24 filename=ene_1.dat
nt/dump 2.siglg.f(2,2,2) $24 filename=ene_2.dat
nt/dump 2.siglg.f(2,3,3) $24 filename=ene_3.dat
nt/dump 2.siglg.f(2,4,4) $24 filename=ene_4.dat
nt/dump 2.siglg.f(2,5,5) $24 filename=ene_5.dat
nt/dump 2.siglg.f(2,6,6) $24 filename=ene_6.dat
nt/dump 2.siglg.f(2,7,7) $24 filename=ene_7.dat
nt/dump 2.siglg.f(2,8,8) $24 filename=ene_8.dat
nt/dump 2.hitraxy.f(-1,3,0,3) $24 filename=x_l.dat
nt/dump 2.hitraxy.f(-1,3,0,2) $24 filename=x_r.dat
nt/dump 2.hitraxy.f(-2,3,0,3) $24 filename=y_l.dat
nt/dump 2.hitraxy.f(-2,3,0,2) $24 filename=y_r.dat
nt/dump 2.hitraxy.f(-3,3,0,3) $24 filename=z_l.dat
nt/dump 2.hitraxy.f(-3,3,0,2) $24 filename=z_r.dat
nt/dump 2.abs(anpower) $24 filename=anp.dat

clo 1
cd //pawc

h/fil 1 run.his ! N
hrout 0

clo 0
cd //pawc

nsim=$HINFO(1010,'ENTRIES')
nlarm=$HINFO(1011,'ENTRIES')
nrarm=$HINFO(1012,'ENTRIES')
n2arm=$HINFO(1013,'ENTRIES')
anp=$HINFO(1023,'MEAN')
anpr=$HINFO(1023,'RMS')
thetastd=$HINFO(1033,'RMS')
phi=$HINFO(1043,'RMS')
tmean=$HINFO(1033,'MEAN')
xlavg=$HINFO(1060,'MEAN')
xlstd=$HINFO(1060,'RMS')
xravg=$HINFO(1061,'MEAN')
xrstd=$HINFO(1061,'RMS')
ylavg=$HINFO(1062,'MEAN')
ylstd=$HINFO(1062,'RMS')
yravg=$HINFO(1063,'MEAN')
yrstd=$HINFO(1063,'RMS')
eanp=$SIGMA([anpr]/SQRT([n2arm]))
acc=$SIGMA(REAL([n2arm])/REAL([nsim]))
eacc=$SIGMA(SQRT([acc]*(1.-[acc])/[nsim]))
accl=$SIGMA(REAL([n2arm])/REAL([nlarm]))
eaccl=$SIGMA(SQRT([accl]*(1.-[accl])/[nlarm]))
accr=$SIGMA(REAL([n2arm])/REAL([nrarm]))
eaccr=$SIGMA(SQRT([accr]*(1.-[accr])/[nrarm]))
err=$SIGMA(100./SQRT([n2arm]))

ve/cr sval(4) r [anp] [acc] [accl] [accr]
ve/cr esval(4) r [eanp] [eacc] [eaccl] [eaccr]

RETURN
